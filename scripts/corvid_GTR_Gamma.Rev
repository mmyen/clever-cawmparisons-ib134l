### Code modified from this link: https://revbayes.github.io/tutorials/ctmc/
# For more information on RevBayes, check out the website: https://revbayes.github.io/

### Read in sequence data for all organisms for the cytb gene.

data = readDiscreteCharacterData("clever-cawmparisons/data/corvids_and_outgroup.nex")

# Get some useful variables from the data. We need these later on.
num_taxa <- data.ntaxa()
num_branches <- 2 * num_taxa - 3
taxa <- data.taxa()

# Make moves and monitors.
moves = VectorMoves()
monitors = VectorMonitors()

### Substitution model: General Time-Reversible (GTR) + Discrete Gamma
# The GTR model is like the HKY model (which assumes unequal stationary frequencies and unequal transition-transversion rates),
# but the exchangeability rates can also differ.
# The Discrete Gamma allows us to account for differences in substitution rates across sites.

# er specifies the exchangeability rates that are drawn from a flat Dirichlet distribution.
er_prior <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_prior)

# er is a stochastic variable, so have to specify moves to propose updates.
moves.append( mvBetaSimplex(er, weight=3) )
moves.append( mvDirichletSimplex(er, weight=1) )

# pi specifies the stationary frequencies that are drawn from a flat Dirichlet distribution.
pi_prior <- v(1,1,1,1)
pi ~ dnDirichlet(pi_prior)

# pi is a stochastic variable, so have to specify moves to propose updates.
moves.append( mvBetaSimplex(pi, weight=2) )
moves.append( mvDirichletSimplex(pi, weight=1) )

# alpha represents the alpha shape parameter for the discrete gamma.
alpha ~ dnUniform(0.0, 10)
# sr holds the set of k rates drawn from the gamma distribution with k rate categories.
# For each site, the likelihood is averaged over the 4 rate sites, as k = 4.
sr := fnDiscretizeGamma( alpha, alpha, 4 ) # 4 bins
moves.append( mvScale(alpha, weight=2.0) )

Q := fnGTR(er, pi)

### Tree model
out_group = clade("Lanius_ludovicianus")

# Prior distribution on the tree topology
topology ~ dnUniformTopology(taxa, outgroup = out_group)
moves.append(mvNNI(topology, weight = num_taxa/1.0)) # NNI (Nearest neighbors interchange) move
# moves.append(mvSPR(topology, weight = num_taxa*2.0)) # SPR (subtree prune and regraft) move

# Branch length prior
for (i in 1:num_branches){
	bl[i] ~ dnExponential(10.0)
	moves.append(mvScale(bl[i]))
}

# Tree length = sum of the branch lengths
TL := sum(bl)

psi := treeAssembly(topology, bl)

### PhyloCTMC Model
# The sequence evolution model uses General Time Reversible matrix with stationary frequencies and exchange rates from pi and er and discrete gamma site rates from sr.
seq ~ dnPhyloCTMC(tree = psi, Q = Q, siteRates = sr, type = "DNA")

# Attach/"clamp" the data
seq.clamp(data)

### Analysis
# Add model
mymodel = model(psi)

# Add monitors
monitors.append( mnScreen(TL, printgen = 100) )
monitors.append( mnFile(psi, filename = "clever-cawmparisons/output_GTR_gamma/corvids_cytb_GTR_gamma.trees", printgen = 1) )
monitors.append( mnModel(filename = "clever-cawmparisons/output_GTR_gamma/corvids_cytb_GTR_gamma.log", printgen = 1 ) )

# Run the analysis
mymcmc = mcmc(mymodel, moves, monitors, nruns = 4, combine = "mixed")
mymcmc.run(generations = 100000, tuningInterval = 100)

### Post-Processingpower posterior analysis is done in a different script (corvid_GTR_Gamma_powp.Rev)
# Analyze tree output by reading in the tree trace
treetrace = readTreeTrace("clever-cawmparisons/output_GTR_gamma/corvids_cytb_GTR_gamma.trees", treetype = "non-clock")
# Generate the maximum a posteriori non-clock tree
map_tree = mapTree(treetrace, "clever-cawmparisons/output_GTR_gamma/corvids_cytb_GTR_gamma_MAP.tree")

#You may want to quit RevBayes now
q()
