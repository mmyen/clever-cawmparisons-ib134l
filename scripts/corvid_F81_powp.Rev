### Code modified from these links: 
#		- https://revbayes.github.io/tutorials/ctmc/
# 		- https://revbayes.github.io/tutorials/model_selection_bayes_factors/bf_subst_model.html
# For more information on RevBayes, check out the website: https://revbayes.github.io/

# Clear workspace to prevent contamination issues across running different scripts.
clear()

### Read in sequence data for all organisms for the cytb gene.

data = readDiscreteCharacterData("clever-cawmparisons/data/corvids_and_outgroup.nex")

# Get some useful variables from the data. We need these later on.
num_taxa <- data.ntaxa()
num_branches <- 2 * num_taxa - 3
taxa <- data.taxa()

# quit()

# Make moves and monitors.
moves = VectorMoves()
monitors = VectorMonitors()

### Substitution Model: Felsenstein 1981 (F81)
# The F81 model assumes unequal stationary frequencies but equal transition-transversion rates.

# pi specifies the stationary frequencies that are drawn from a flat Dirichlet distribution.
pi_prior <- v(1,1,1,1)
pi ~ dnDirichlet(pi_prior)

# pi is a stochastic variable, so have to specify moves to propose updates.
moves.append( mvBetaSimplex(pi, weight=2) )
moves.append( mvDirichletSimplex(pi, weight=1) )

Q := fnF81(pi)

### Tree model
out_group = clade("Lanius_ludovicianus")

# Prior distribution on the tree topology
topology ~ dnUniformTopology(taxa, outgroup = out_group)
moves.append(mvNNI(topology, weight = num_taxa/1.0)) # NNI (Nearest neighbors interchange) move
# moves.append(mvSPR(topology, weight = num_taxa*2.0)) # SPR (subtree prune and regraft) move

# Branch length prior
for (i in 1:num_branches){
	bl[i] ~ dnExponential(10.0)
	moves.append(mvScale(bl[i]))
}

# Tree length = sum of the branch lengths
TL := sum(bl)

psi := treeAssembly(topology, bl)

### PhyloCTMC Model

# The sequence evolution model uses the Felsenstein 1981 matrix with starting stationary frequencies from pi.
seq ~ dnPhyloCTMC(tree = psi, Q = Q, type = "DNA")

# Attach/"clamp" the data
seq.clamp(data)

### Analysis
# Add model
mymodel = model(psi)

# Add monitors
monitors.append( mnScreen(TL, printgen = 100) )
# monitors.append( mnFile(psi, filename = "clever-cawmparisons/output_F81/corvids_cytb_F81.trees", printgen=1) )
# monitors.append( mnModel(filename = "clever-cawmparisons/output_F81/corvids_cytb_F81.log", printgen = 1) )

# Set up power posterior
pow_p = powerPosterior(mymodel, moves, monitors, "clever-cawmparisons/output_F81/powp_F81.out", cats=50)

# Run power posterior
pow_p.burnin(generations=10000,tuningInterval=1000)
pow_p.run(generations=1000)

# Stepping stone analysis
ss = steppingStoneSampler(file="clever-cawmparisons/output_F81/powp_F81.out", powerColumnName="power", likelihoodColumnName="likelihood")
ss.marginal()

# Path sampling analysis
ps = pathSampler(file="clever-cawmparisons/output_F81/powp_F81.out", powerColumnName="power", likelihoodColumnName="likelihood")
ps.marginal()

