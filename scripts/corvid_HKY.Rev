### Code modified from this link: https://revbayes.github.io/tutorials/ctmc/
# For more information on RevBayes, check out the website: https://revbayes.github.io/

### Read in sequence data for all organisms for the cytb gene.

data = readDiscreteCharacterData("clever-cawmparisons/data/corvids_and_outgroup.nex")

# Get some useful variables from the data. We need these later on.
num_taxa <- data.ntaxa()
num_branches <- 2 * num_taxa - 3
taxa <- data.taxa()

# Make moves and monitors.
moves = VectorMoves()
monitors = VectorMonitors()

### Substitution model: Hasegawa-Kishino-Yano (HKY)
# The HKY model assumes unequal stationary frequencies and unequal transition-transversion rates.

# pi specifies the stationary frequencies that are drawn from a flat Dirichlet distribution.
pi_prior <- v(1, 1, 1, 1)
pi ~ dnDirichlet(pi_prior) 

# pi is a stochastic variable, so have to specify moves to propose updates.
moves.append( mvBetaSimplex(pi, weight=2) )
moves.append( mvDirichletSimplex(pi, weight=1) )

# kappa specifies ratio of transition-transversion rates. kappa is drawn from a log normal distribution.
kappa ~ dnLognormal(0.0,1.0)

# We need to specify a move for kappa as well.
moves.append( mvScale(kappa) )

Q := fnHKY(kappa, pi)

### Tree model
out_group = clade("Lanius_ludovicianus")

# Prior distribution on the tree topology
topology ~ dnUniformTopology(taxa, outgroup = out_group)
moves.append(mvNNI(topology, weight = num_taxa/1.0)) # NNI (Nearest neighbors interchange) move
# moves.append(mvSPR(topology, weight = num_taxa*2.0)) # SPR (subtree prune and regraft) move

# Branch length prior
for (i in 1:num_branches){
	bl[i] ~ dnExponential(10.0)
	moves.append(mvScale(bl[i]))
}

# Tree length = sum of the branch lengths
TL := sum(bl)

psi := treeAssembly(topology, bl)

### PhyloCTMC Model

# The sequence evolution model uses the Hasegawa-Kishino-Yano matrix with starting stationary frequences from pi_prior and starting transition-transversion rate from the kappa random variable.
seq ~ dnPhyloCTMC(tree = psi, Q = Q, type = "DNA")

# Attach/"clamp" the data
seq.clamp(data)

### Analysis
# Add model
mymodel = model(psi)

# Add monitors
monitors.append( mnScreen(TL, printgen = 100) )
monitors.append( mnFile(psi, filename = "clever-cawmparisons/output_HKY/corvids_cytb_HKY.trees", printgen=1) )
monitors.append( mnModel(filename = "clever-cawmparisons/output_HKY/corvids_cytb_HKY.log", printgen = 1) )

# Run the analysis
mymcmc = mcmc(mymodel, moves, monitors, nruns = 4, combine = "mixed")
mymcmc.run(generations = 100000, tuningInterval = 100)


### Post-Processing: power posterior analysis is done in a different script (corvid_HKY_powp.Rev)
# Analyze tree output by reading in the tree trace
treetrace = readTreeTrace("clever-cawmparisons/output_HKY/corvids_cytb_HKY.trees", treetype = "non-clock")
# Generate the maximum a posteriori non-clock tree
map_tree = mapTree(treetrace, "clever-cawmparisons/output_HKY/corvids_cytb_HKY_MAP.tree")

#You may want to quit RevBayes now
q()

