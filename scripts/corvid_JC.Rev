### Code modified from this link: https://revbayes.github.io/tutorials/ctmc/
# For more information on RevBayes, check out the website: https://revbayes.github.io/

### Read in sequence data for all organisms for the cytb gene.

data = readDiscreteCharacterData("clever-cawmparisons/data/corvids_and_outgroup.nex")

# Get some useful variables from the data. We need these later on.
num_taxa <- data.ntaxa()
num_branches <- 2 * num_taxa - 3
taxa <- data.taxa()

# Make moves and monitors.
moves = VectorMoves()
monitors = VectorMonitors()

### Substitution Model: Jukes-Cantor
# The Jukes-Cantor model assumes equal stationary frequencies and equal mutation rates.
Q <- fnJC(4)

### Tree Model
out_group = clade("Lanius_ludovicianus")

# Prior distribution on the tree topology
topology ~ dnUniformTopology(taxa, outgroup = out_group)
moves.append(mvNNI(topology, weight = num_taxa/1.0)) # NNI (Nearest neighbors interchange) move
# moves.append(mvSPR(topology, weight = num_taxa*2.0)) # SPR (subtree prune and regraft) move

# Branch length prior
for (i in 1:num_branches){
	bl[i] ~ dnExponential(10.0)
	moves.append(mvScale(bl[i]))
}

# Tree length = sum of the branch lengths
TL := sum(bl)

psi := treeAssembly(topology, bl)

### PhyloCTMC Model

# The sequence evolution model uses the Jukes-Cantor matrix with equal base frequencies and equal mutation rates.

# sequence evolution model: uses Jukes-Cantor matrix
seq ~ dnPhyloCTMC(tree = psi, Q = Q, type = "DNA")

# Attach/"clamp" the data
seq.clamp(data)

### Analysis
# Add model
mymodel = model(psi)


# Add monitors
monitors.append( mnScreen(TL, printgen = 100) )
monitors.append( mnFile(psi, filename = "clever-cawmparisons/output_JC/corvids_cytb_JC.trees", printgen=1) )
monitors.append( mnModel(filename = "clever-cawmparisons/output_JC/corvids_cytb_JC.log", printgen = 1) )

# Run the analysis
mymcmc = mcmc(mymodel, moves, monitors, nruns = 4, combine = "mixed")
mymcmc.run(generations = 100000, tuningInterval = 100)

### Post-Processing: power posterior analysis is done in a different script (corvid_JC_powp.Rev)
# Analyze tree output by reading in the tree trace
treetrace = readTreeTrace("clever-cawmparisons/output_JC/corvids_cytb_JC.trees", treetype = "non-clock")
# Generate the maximum a posteriori non-clock tree
map_tree = mapTree(treetrace, "clever-cawmparisons/output_JC/corvids_cytb_JC_MAP.tree")

# You may want to quit RevBayes now
q()

